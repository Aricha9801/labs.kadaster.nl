prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix uom: <http://www.opengis.net/def/uom/OGC/1.0/>
prefix verblijfsobject: <http://bag.basisregistraties.overheid.nl/bag/id/verblijfsobject/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select
  (?pand1Shape as ?coffeeshopShape)
  (concat('<h3>Coffeeshop</h3>',str(?pand1ShapeLabel)) as ?coffeeshopShapeLabel)
  (?pand2Shape as ?schoolShape)
  (concat('<h3>School</h3>',?pand2ShapeLabel) as ?schoolShapeLabel)
  ?afstandShape
  ?afstandShapeLabel
{
  {
    select * {
      bind(verblijfsobject:0307010000462567 as ?verblijfsobject1)
      bind(verblijfsobject:0307010000519401 as ?verblijfsobject2)
      # verblijfsobject 1
      graph ?verblijfsobject1Voorkomen {
        ?verblijfsobject1
          bag:pandrelatering ?pand1;
          geo:hasGeometry/geo:asWKT ?verblijfsobject1Shape;
          rdfs:label ?verblijfsobject1Label.
      }
      filter not exists {
        ?verblijfsobject1Voorkomen bag:eindGeldigheid _:1.
      }
      graph ?pand1Voorkomen {
        ?pand1
          geo:hasGeometry/geo:asWKT ?pand1Shape;
          rdfs:label ?pand1Label.
      }
      filter not exists {
        ?pand1Voorkomen bag:eindGeldigheid _:2.
      }
      bind(strdt(concat(
          '<dl>',
          '<dt>Pand</dt>',
          '<dd><a href="',str(?pand1),'">',str(?pand1Label),'</a></dd>',
          '<dt>Verblijfsobject</dt>',
          '<dd><a href="',str(?verblijfsobject1),'">',str(?verblijfsobject1Label),'</a></dd>',
          '</dl>').rdf:HTMl) as ?pand1ShapeLabel)
      # verblijfsobject 2
      graph ?verblijfsobject2Voorkomen {
        ?verblijfsobject2
          bag:pandrelatering ?pand2;
          geo:hasGeometry/geo:asWKT ?verblijfsobject2Shape;
          rdfs:label ?verblijfsobject2Label.
      }
      filter not exists {
        ?verblijfsobject2Voorkomen bag:eindGeldigheid _:3.
      }
      graph ?pand2Voorkomen {
        ?pand2
          geo:hasGeometry/geo:asWKT ?pand2Shape;
          rdfs:label ?pand2Label.
      }
      filter not exists {
        ?pand2Voorkomen bag:eindGeldigheid _:4.
      }
      bind(strdt(concat(
          '<dl>',
          '<dt>Pand</dt>',
          '<dd><a href="',str(?pand2),'">',str(?pand2Label),'</a></dd>',
          '<dt>Verblijfsobject</dt>',
          '<dd><a href="',str(?verblijfsobject2),'">',str(?verblijfsobject2Label),'</a></dd>',
          '</dl>').rdf:HTMl) as ?pand2ShapeLabel)
      # afstand
      bind(concat('LineString (',replace(str(?verblijfsobject1Shape),'POINT\\s\\((.*)\\)','$1'),', ',replace(str(?verblijfsobject2Shape),'POINT\\s\\((.*)\\)','$1'),')') as ?afstandShape)
      bind(round(geof:distance(?verblijfsobject1Shape, ?verblijfsobject2Shape, uom:metre)) as ?afstand)
      bind(strdt(concat(
          '<h3>Hemelsbrede afstand tussen coffeeshop en school</h3>',
          '<p>',str(?afstand),' m</p>'),rdf:HTML) as ?afstandShapeLabel)
    }
  }
}
