prefix def: <http://data.oudlisse.nl/def#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select
  ?shape
  (concat('<table style="cell-margin: 10; width: 100%;" border="1">',
          "<h2>Bewoners van 't Hoogje</h2>",
          '<tr><th>Naam</th><th>beroep</th><th>leeftijd</th><th>gezindte</th></tr>',
          group_concat(distinct ?row; separator=''),
          '</table>') as ?shapeLabel)
{
  ?perceel a def:Perceel;
    def:nummerPerceel "C191";
    geo:asWKT ?shape.
  ?bewoners
    def:beroep ?beroep;
    def:gezindte ?gezindte;
    def:leeftijd ?leeftijd;
    def:woontOpAdres/def:gekoppeldAanPerceel ?perceel;
    rdfs:label ?fullName.
  bind(concat('<tr>',
              '<td>',str(?fullName),'</td>',
              '<td>',if(bound(?beroep),str(?beroep),''),'</td>',
              '<td>',if(bound(?leeftijd),str(?leeftijd),''),'</td>',
              '<td>',if(bound(?gezindte),str(?gezindte),''),'</td>',
              '</tr>') as ?row)
}
group by ?shape
