prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select
  ?shape
  (strdt(concat(
    '<h1>',IF(STRLEN(GROUP_CONCAT(DISTINCT ?naam;separator=', ')),GROUP_CONCAT(DISTINCT ?naam;separator=', '),'Geen Naam'),'</h1>','<dl>',
    '<dt>Type(s)</dt><dd>',GROUP_CONCAT(DISTINCT ?type;separator=', '),'</dd>',
    '<dt>Functie(s)</dt><dd>',GROUP_CONCAT(DISTINCT ?functies;separator=', '),'</dd>',
    '<dt>Nummer(s)</dt><dd>',GROUP_CONCAT(?href;separator=', '),'</dd>'),rdf:HTML) as ?shapeLabel)
{
  {
    select
      (group_concat(distinct ?functie;separator=', ') as ?functies)
	  ?naam
      ?shape
      ?type
      ?href
    {
      ?monument
        ceo:rijksmonumentnummer ?nummer;
        ceo:heeftLocatieAanduiding/ceo:heeftLocatieAdres/ceo:heeftGemeente <http://standaarden.overheid.nl/owms/terms/Lisse_(gemeente)>;
        ceo:heeftGeometrie/geo:asWKT ?shape;
        ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie.
      optional { ?monument ceo:heeftNaam/ceo:naam ?naam. }
      optional { ?monument ceo:heeftType/ceo:heeftTypeNaam/skos:prefLabel ?type. }
      BIND(concat('<a href="',str(?monument),'" target="_blank">',str(?nummer),'</a>') as ?href)
    }
    group by ?naam ?href ?shape ?type
  }
}
GROUP BY ?shape
ORDER BY DESC(?shapeLabel)
