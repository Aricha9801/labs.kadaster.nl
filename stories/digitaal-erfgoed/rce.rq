prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
select
  ?shape
  (concat(
    '<h1>',if(bound(?naam),str(?naam),'Geen naam'),'</h1>',
    '<dl>',
    if(bound(?type),concat('<dt>Type</dt><dd>',str(?type),'</dd>'),''),
    '<dt>Functies</dt><dd>',str(?functies),'</dd>',
    '<dt>Nummer</dt><dd><a href="',str(?monument),'" target="_blank">',str(?nummer),'</a></dd>') as ?shapeLabel)
{
  {
    select
      (group_concat(distinct ?functie;separator=', ') as ?functies)
      ?monument
      ?naam
      ?nummer
      ?shape
      ?type
    {
      ?monument
        ceo:rijksmonumentnummer ?nummer;
        ceo:heeftLocatieAanduiding/ceo:heeftLocatieAdres/ceo:heeftGemeente <http://standaarden.overheid.nl/owms/terms/Lisse_(gemeente)>;
        ceo:heeftGeometrie/geo:asWKT ?shape;
        ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie.
      optional { ?monument ceo:heeftNaam/ceo:naam ?naam. }
      optional { ?monument ceo:heeftType/ceo:heeftTypeNaam/skos:prefLabel ?type. }
    }
    group by ?monument ?naam ?nummer ?shape ?type
  }
}
