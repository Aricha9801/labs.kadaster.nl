prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix wdn: <http://www.wikidata.org/prop/direct-normalized/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select (strdt(CONCAT(?widgetPart1,str(SAMPLE(?img)),?widgetPart2),rdf:HTML) as ?widget ) {
  {
    select
    (group_concat(distinct ?functie;
        separator=', ') as ?functies)
    ?monument
    ?naam
    ?nummer
    ?type
    ?omschrijving
    {
      ?monument
        ceo:rijksmonumentnummer ?nummer;
        ceo:heeftLocatieAanduiding/ceo:heeftLocatieAdres/ceo:heeftGemeente <http://standaarden.overheid.nl/owms/terms/Lisse_(gemeente)>;
        ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie;
        ceo:heeftNaam/ceo:naam ?naam.
      optional {
        ?monument ceo:heeftType/ceo:heeftTypeNaam/skos:prefLabel ?type.
      }
      optional {
        ?monument ceo:heeftOmschrijving/ceo:omschrijving ?omschrijving .
      }
    }
    group by ?monument ?naam ?nummer ?type ?omschrijving
  } {
    service <https://query.wikidata.org/sparql> {
      SELECT * WHERE {
        ?wikidata
          wdt:P359 ?nummerWiki;
          wdt:P131 <http://www.wikidata.org/entity/Q332730> .
        OPTIONAL{
          ?wikidata wdt:P18 ?img ;
                    }
        OPTIONAL {
          ?wikidata rdfs:label ?label .
          FILTER(lang(?label) = "nl")
        }
      }
    }
  }
  FILTER(?nummer = xsd:integer(?nummerWiki))
  bind(concat('https://monumentenregister.cultureelerfgoed.nl/monumenten/',str(?nummer)) as ?url)
  bind(concat(
      '<div style="min-width: 18vw;max-height: none;height: auto;"><h1><a href="',str(?url),'" target="_blank">',if(bound(?naam),str(?naam),'Geen naam'),'</a></h1><a href="',str(?wikidata),'" target="_blank"><img src="') as ?widgetPart1)
  bind(concat(
      '"></a><p style="max-height: 100%;">',if(strlen(?omschrijving)>370, concat(substr(str(?omschrijving),1,370),'... <a href="',str(?url),'" target="_blank">Toon meer</a>'),?omschrijving),'</p></div>') as ?widgetPart2)
}
GROUP BY ?widgetPart2 ?widgetPart1
