prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix wd: <http://www.wikidata.org/entity/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
select * {
  ?windfarm
    wdt:P31 wd:Q194356; # windfarm
    wdt:P17 wd:Q55; # Netherlands
    wdt:P625 ?shape;
    rdfs:label ?label.
  optional {
    ?windfarm wdt:P18 ?image.
  }
  bind(if(bound(?image),
          concat('<figure><img src="',str(?image),'"><figcaption>',str(?label),'</figcaption></figure>'),
          str(?label)) as ?content)
  bind('''<a href="{{windfarm}}" target="_blank">{{content}}</a>'''^^rdf:HTML as ?shapeLabel)
}
