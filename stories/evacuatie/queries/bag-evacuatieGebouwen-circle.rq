PREFIX brt-begrip: <http://brt.basisregistraties.overheid.nl/id/begrip/>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX bagPand: <http://bag.basisregistraties.overheid.nl/bag/id/pand/>
prefix bag-begrip: <http://bag.basisregistraties.overheid.nl/id/begrip/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geosf: <http://www.opengis.net/def/function/geosparql/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX cbs: <https://data.pldn.nl/cbs/wijken-buurten/def/cbs#>
PREFIX cbs-concept: <https://data.pldn.nl/cbs/wijken-buurten/cbs/id/concept/>
SELECT ?bagPandInRadius (str(?afstandNum) as ?afstand) ?type (str(?bouwjaar) as ?bouwjaar) (str(?oppervlakte) as ?oppervlakte) ?adres WHERE {
  {
    SELECT DISTINCT ?bagPandInRadius ?type (str(?bouwjaar) as ?bouwjaar) (str(?oppervlakte) as ?oppervlakte) ?adres ?afstandNum ?radius WHERE {
      {
        SELECT  ?radius ?pointAccident ?circle WHERE {
          # Set the radius in km.
          bind(5 as ?radius)
          # Set PointAccident ifyou want to use a PointGeometry. Important is that the other part of this query needs to be removed. between the lines: =====
          # values (?pointAccident) {
          #   ("POINT(4.58623 56.1928374)")
          # }
          #
          # Set the values for Woonplaats, straatnaam and huisnummer of the accident.
          # REMOVE FROM HERE: =====================================
          values (?woonPlaatsAccident ?StraatNaamAccident ?huisNummerAccident ?huisNummerLetterAccident) {
            ("Amsterdam"  "Carolina MacGillavrylaan"  UNDEF UNDEF )
          }
          # Or set the values for postcode and huisnummer of the accident.
          values (?postcodeAccident ?huisNummerAccident ?huisNummerLetterAccident)
          {
            ("1098XA" 24 UNDEF )
          }
          OPTIONAL {
            FILTER(?woonPlaatsAccident &&?StraatNaamAccident && ?huisNummerAccident)
            # Get the information from the object.
            ?NummerAanduidingAccident bag:huisnummer ?huisNummerAccident;
                                      bag:bijbehorendeOpenbareRuimte ?openbareRuimteAccident .
            optional {
              ?NummerAanduidingAccident bag:huisletter ?huisNummerLetterAccident.
            }
            ?openbareRuimteAccident bag:naamOpenbareRuimte ?StraatNaamAccident ;
                                    bag:bijbehorendeWoonplaats [ bag:naamWoonplaats ?woonPlaatsAccident ] .
            ?bagPandAccident bag:hoofdadres ?NummerAanduidingAccident ;
                             geo:hasGeometry/geo:asWKT ?pointAccident .
          }
          OPTIONAL {
            FILTER(?postcodeAccident && ?huisNummerAccident)
            # Get the information from the object.
            ?NummerAanduidingAccident bag:postcode ?postcodeAccident;
                                      bag:huisnummer ?huisNummerAccident.
            optional {
              ?NummerAanduidingAccident bag:huisletter ?huisNummerLetterAccident.
            }
            ?bagPandAccident bag:hoofdadres ?NummerAanduidingAccident ;
                             geo:hasGeometry/geo:asWKT ?pointAccident .
          }
          # REMOVE TO HERE: ==============================================
          bind(xsd:double(strbefore(strafter(str(?pointAccident),"POINT(")," ")) as ?x)
          bind(xsd:double(strbefore(strafter(strafter(str(?pointAccident),"POINT(")," "),")")) as ?y)
          bind((?radius/230.0)*1 as ?r1)
          bind((?radius/230.0)*0.866 as ?r2)
          bind((?radius/230.0)*0.707 as ?r3)
          bind((?radius/230.0)*0.5 as ?r4)
          bind((?radius/230.0)*0 as ?r5)
          BIND(strdt(concat(
                "POLYGON ((",str(?x+1.8*?r1)," ",str(?y+?r5)," , ",str(?x+1.8*?r2)," ",str(?y+?r4)," , ",str(?x+1.8*?r3)," ",str(?y+?r3)," , ",str(?x+1.8*?r4)," ",str(?y+?r2)," ,",
                str(?x-1.8*?r5)," ",str(?y+?r1)," , ",str(?x-1.8*?r4)," ",str(?y+?r2)," , ",str(?x-1.8*?r3)," ",str(?y+?r3)," , ",str(?x-1.8*?r2)," ",str(?y+?r4)," ,",
                str(?x-1.8*?r1)," ",str(?y-?r5)," , ",str(?x-1.8*?r2)," ",str(?y-?r4)," , ",str(?x-1.8*?r3)," ",str(?y-?r3)," , ",str(?x-1.8*?r4)," ",str(?y-?r2)," ,",
                str(?x+1.8*?r5)," ",str(?y-?r1)," , ",str(?x+1.8*?r4)," ",str(?y-?r2)," , ",str(?x+1.8*?r3)," ",str(?y-?r3)," , ",str(?x+1.8*?r2)," ",str(?y-?r4),"))")
              ,<http://www.openlinksw.com/schemas/virtrdf#Geometry>) as ?circle)
        }
        limit 1
      }
      # Get the BAG panden in the radius
      ?bagPandInRadius geo:hasGeometry/geo:asWKT ?locationSurround ;
                       bag:oorspronkelijkBouwjaar ?bouwjaar ;
                       a bag:Pand ;
                       bag:status bag-begrip:PandInGebruik .
      FILTER( bif:st_may_intersect( ?circle, ?locationSurround) )
      BIND(str(?locationSurround) as ?substringSplit)
      BIND(IF((bif:GeometryType(?locationSurround) = "POLYGON"),strdt(concat("POINT(",strafter(strbefore(?substringSplit,","),"(("),")"),<http://www.openlinksw.com/schemas/virtrdf#Geometry>),UNDEF) as ?FirstPointlocationSurroundP1)
      BIND(IF((bif:GeometryType(?locationSurround) = "MULTIPOLYGON"),strdt(concat("POINT(",strafter(strbefore(?substringSplit,","),"((("),")"),<http://www.openlinksw.com/schemas/virtrdf#Geometry>),UNDEF) as ?FirstPointlocationSurroundP2)
      BIND(IF(((bif:GeometryType(?locationSurround) = "POINT")),
          bif:haversine_deg_km(bif:st_x(?locationSurround),bif:st_y(?locationSurround) ,bif:st_x(?pointAccident),bif:st_y(?pointAccident)),
          IF(((bif:GeometryType(?FirstPointlocationSurroundP1) = "POINT")),
            bif:haversine_deg_km(bif:st_x(?FirstPointlocationSurroundP1),bif:st_y(?FirstPointlocationSurroundP1) ,bif:st_x(?pointAccident),bif:st_y(?pointAccident)),
            IF(((bif:GeometryType(?FirstPointlocationSurroundP2) = "POINT")),
              bif:haversine_deg_km(bif:st_x(?FirstPointlocationSurroundP2),bif:st_y(?FirstPointlocationSurroundP2) ,bif:st_x(?pointAccident),bif:st_y(?pointAccident)),
              ?radius+0.01
            )
          )
        )
        as ?afstandNum)
      ?verblijfsObject
        bag:hoofdadres ?nummeraanduiding;
        bag:oppervlakte ?oppervlakte;
        bag:pandrelatering ?bagPandInRadius ;
        rdf:type  [ dct:subject [ skos:notation ?type ]] .
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                        bag:huisnummer ?huisnummer;
                        bag:postcode ?postcode.
      ?openbareRuimte bag:naamOpenbareRuimte ?straatNaam .
      #  # De huisletter moet worden gematched, maar alleen als deze ook is ingesteld
      #  # middels bovenstaande `bind' clausule.
      optional {
        ?nummeraanduiding bag:huisletter ?huisletter.
      }
      BIND(concat(?straatNaam, " ", ?huisnummer, ?huisletter, " ", ?postcode) as ?adres)
    }
    ORDER BY ?afstandNum
    limit 10000
  }
  FILTER(?afstandNum < ?radius)
}
