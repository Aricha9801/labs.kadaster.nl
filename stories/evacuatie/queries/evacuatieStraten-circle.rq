PREFIX brt-begrip: <http://brt.basisregistraties.overheid.nl/id/begrip/>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX brt-wegdeel: <http://brt.basisregistraties.overheid.nl/top10nl/id/wegdeel/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix bag-begrip: <http://bag.basisregistraties.overheid.nl/id/begrip/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geosf: <http://www.opengis.net/def/function/geosparql/>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?StraatInRadius ?type ?distance (substr(str(?distance),0,4) as ?distanceSTR) ?StreetShape ?straatNaam (GROUP_CONCAT(CONCAT(?typePand, " : ",?n);SEPARATOR="</li><li>") as ?StreetShapeLabelPart) ?StreetShapeLabel WHERE {
  {
    SELECT ?StraatInRadius ?type ?distance ?StreetShape ?straatNaam ?typePand (COUNT(?verblijfsObject) as ?n) ?radius  WHERE {
      {
        SELECT DISTINCT ?StraatInRadius ?type ?distance ?locationSurround ?straatNaam ?radius WHERE {
          {
            SELECT  ?radius ?pointAccident ?circle WHERE {
              # Set the radius in km.
              bind(5 as ?radius)
              # Set PointAccident ifyou want to use a PointGeometry. Important is that the other part of this query needs to be removed. between the lines: =====
              # values (?pointAccident) {
              #   ("POINT(4.58623 56.1928374)")
              # }
              #
              # Set the values for Woonplaats, straatnaam and huisnummer of the accident.
              # REMOVE FROM HERE: =====================================
              values (?woonPlaatsAccident ?StraatNaamAccident ?huisNummerAccident ?huisNummerLetterAccident) {
                ("Amsterdam"  "Carolina MacGillavrylaan"  UNDEF UNDEF )
              }
              # Or set the values for postcode and huisnummer of the accident.
              values (?postcodeAccident ?huisNummerAccident ?huisNummerLetterAccident)
              {
                ("1098XA" 24 UNDEF )
              }
              OPTIONAL {
                FILTER(?woonPlaatsAccident &&?StraatNaamAccident && ?huisNummerAccident)
                # Get the information from the object.
                ?NummerAanduidingAccident bag:huisnummer ?huisNummerAccident;
                                          bag:bijbehorendeOpenbareRuimte ?openbareRuimteAccident .
                optional {
                  ?NummerAanduidingAccident bag:huisletter ?huisNummerLetterAccident.
                }
                ?openbareRuimteAccident bag:naamOpenbareRuimte ?StraatNaamAccident ;
                                        bag:bijbehorendeWoonplaats [ bag:naamWoonplaats ?woonPlaatsAccident ] .
                ?bagPandAccident bag:hoofdadres ?NummerAanduidingAccident ;
                                 geo:hasGeometry/geo:asWKT ?pointAccident .
              }
              OPTIONAL {
                FILTER(?postcodeAccident && ?huisNummerAccident)
                # Get the information from the object.
                ?NummerAanduidingAccident bag:postcode ?postcodeAccident;
                                          bag:huisnummer ?huisNummerAccident.
                optional {
                  ?NummerAanduidingAccident bag:huisletter ?huisNummerLetterAccident.
                }
                ?bagPandAccident bag:hoofdadres ?NummerAanduidingAccident ;
                                 geo:hasGeometry/geo:asWKT ?pointAccident .
              }
              # REMOVE TO HERE: ==============================================
              bind(xsd:double(strbefore(strafter(str(?pointAccident),"POINT(")," ")) as ?x)
              bind(xsd:double(strbefore(strafter(strafter(str(?pointAccident),"POINT(")," "),")")) as ?y)
              bind((?radius/230.0)*1 as ?r1)
              bind((?radius/230.0)*0.866 as ?r2)
              bind((?radius/230.0)*0.707 as ?r3)
              bind((?radius/230.0)*0.5 as ?r4)
              bind((?radius/230.0)*0 as ?r5)
              BIND(strdt(concat(
                    "POLYGON ((",str(?x+1.8*?r1)," ",str(?y+?r5)," , ",str(?x+1.8*?r2)," ",str(?y+?r4)," , ",str(?x+1.8*?r3)," ",str(?y+?r3)," , ",str(?x+1.8*?r4)," ",str(?y+?r2)," ,",
                    str(?x-1.8*?r5)," ",str(?y+?r1)," , ",str(?x-1.8*?r4)," ",str(?y+?r2)," , ",str(?x-1.8*?r3)," ",str(?y+?r3)," , ",str(?x-1.8*?r2)," ",str(?y+?r4)," ,",
                    str(?x-1.8*?r1)," ",str(?y-?r5)," , ",str(?x-1.8*?r2)," ",str(?y-?r4)," , ",str(?x-1.8*?r3)," ",str(?y-?r3)," , ",str(?x-1.8*?r4)," ",str(?y-?r2)," ,",
                    str(?x+1.8*?r5)," ",str(?y-?r1)," , ",str(?x+1.8*?r4)," ",str(?y-?r2)," , ",str(?x+1.8*?r3)," ",str(?y-?r3)," , ",str(?x+1.8*?r2)," ",str(?y-?r4),"))")
                  ,<http://www.openlinksw.com/schemas/virtrdf#Geometry>) as ?circle)
            }
            limit 1
          }
          # Get the BAG panden in the radius
          ?StraatInRadius brt:hartGeometrie/geo:asWKT ?locationSurround ;
                          a brt:Wegdeel, [ rdfs:subClassOf brt:Wegdeel ;
                                           dct:subject [ skos:notation ?type ]] ;
                                                         brt:status brt-begrip:InGebruik ;
                                                         brt:naam ?straatNaam .
          FILTER( bif:st_intersects( ?circle, ?locationSurround) )
          BIND(IF((bif:GeometryType(?locationSurround) = "LINESTRING"),strdt(concat("POINT(",strafter(strbefore(str(?locationSurround),","),"("),")"),<http://www.openlinksw.com/schemas/virtrdf#Geometry>),UNDEF) as ?FirstPointlocationSurroundP1)
          BIND(IF((bif:GeometryType(?locationSurround) = "POLYGON"),strdt(concat("POINT(",strafter(strbefore(str(?locationSurround),","),"(("),")"),<http://www.openlinksw.com/schemas/virtrdf#Geometry>),UNDEF) as ?FirstPointlocationSurroundP2)
          BIND(IF(((bif:GeometryType(?locationSurround) = "POINT")),
              bif:haversine_deg_km(bif:st_x(?locationSurround),bif:st_y(?locationSurround) ,bif:st_x(?pointAccident),bif:st_y(?pointAccident)),
              IF(((bif:GeometryType(?FirstPointlocationSurroundP1) = "POINT")),
                bif:haversine_deg_km(bif:st_x(?FirstPointlocationSurroundP1),bif:st_y(?FirstPointlocationSurroundP1) ,bif:st_x(?pointAccident),bif:st_y(?pointAccident)),
                IF(((bif:GeometryType(?FirstPointlocationSurroundP2) = "POINT")),
                  bif:haversine_deg_km(bif:st_x(?FirstPointlocationSurroundP2),bif:st_y(?FirstPointlocationSurroundP2) ,bif:st_x(?pointAccident),bif:st_y(?pointAccident)),
                  ?radius+0.01
                )
              )
            )
            as ?distance)
        }
        ORDER BY ?distance
      }
      BIND(?locationSurround as ?StreetShape)
      BIND(str(?straatNaam) as ?strStraatNaam)
      ?verblijfsObject bag:hoofdadres ?nummeraanduiding;
                       rdf:type  [ dct:subject [ skos:notation ?typePand ]] .
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte .
      ?openbareRuimte bag:naamOpenbareRuimte ?strStraatNaam .
    }
    GROUP BY ?StraatInRadius ?type ?distance ?StreetShape ?straatNaam ?typePand ?radius
    ORDER BY ?distance
  }
  FILTER (?distance < ?radius)
  BIND(strdt('''<h3>{{straatNaam}}</h3>
<dd>
<dl>
<dt>Type weg</dt>
<dd>{{type}}</dd>
<dt>Afstand tot calamiteit</dt>
<dd>{{distanceSTR}} km</dd>
</dl>
</dd>
<ul><li>{{StreetShapeLabelPart}}</li></ul>
''',rdf:HTML) as ?StreetShapeLabel)
}
GROUP BY ?StraatInRadius ?type ?distance ?StreetShape ?straatNaam ?StreetShapeLabel
ORDER BY ?distance
LIMIT 750
