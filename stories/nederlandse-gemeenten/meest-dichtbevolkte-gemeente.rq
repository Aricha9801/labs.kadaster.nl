prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?woonplaatsShape ?woonplaatsShapeLabel {
  ?woonplaats1
    brt:isBAGwoonplaats true;
    geo:hasGeometry/geo:asWKT ?woonplaatsShape;
    rdfs:label ?woonplaatsNaam.
  service <https://dbpedia.org/sparql> {
    ?woonplaats2
      foaf:depiction ?vlag;
      rdfs:label ?woonplaatsNaam.
  }
  bind(concat(
    '<h2>',str(?woonplaatsNaam),'</h2>',
    if(bound(?vlag),concat('<img src="',replace(str(?vlag),'http://','https://'),'" style="max-width: 125px;">'),''),
    '<p>Link naar DBpedia: <a href="',str(?woonplaats2),'" target="_blank">link</a></p>') as ?woonplaatsShapeLabel)
}
limit 25
