prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
select
  (?cLabel as ?klasse)
  (?denomination as ?denominatie)
  (?subdenomination as ?subdenominatie)
  (sum(?n) as ?n)
{
  {
    select ?cLabel ?string ?subdenomination ?n {
      {
        select ?cLabel ?first ?second ?third ?fourth (count(?gebouw) as ?n) {
          values (?c ?cLabel) {
            ( brt:Kerk "Kerk" )
            ( brt:Moskee "Moskee" )
            ( brt:OverigReligieusGebouw "Overig Religieus Gebouw" )
            ( brt:Synagoge "Synagoge" )
          }
          ?gebouw a ?c;
            brt:soortnaam ?denomString.
          bind(" & " as ?and)
          {
            # 1 denomination
            filter(!regex(?denomString, ?and))
            bind(?denomString as ?first)
          } union {
            # >1 denomination
            filter(regex(?denomString, ?and))
            bind(strbefore(?denomString, ?and) as ?first)
            bind(strafter(?denomString, ?and) as ?rest1)
            {
              # 2 denominations
              filter(!regex(?rest1, ?and))
              bind(?rest1 as ?second)
            } union {
              # >2 denominations
              filter(regex(?rest1, ?and))
              bind(strbefore(?rest1, ?and) as ?second)
              bind(strafter(?rest1, ?and) as ?rest2)
              {
                # 3 denominations
                filter(!regex(?rest2, ?and))
                bind(?rest2 as ?third)
              } union {
                # 4 denominations
                filter(regex(?rest2, ?and))
                bind(strbefore(?rest2, ?and) as ?third)
                bind(strafter(?rest2, ?and) as ?fourth)
              }
            }
          }
        }
        group by ?cLabel ?first ?second ?third ?fourth
      }
      {
        bind(?first as ?string)
      } union {
        filter(bound(?second))
        bind(?second as ?string)
      } union {
        filter(bound(?third))
        bind(?third  as ?string)
      } union {
        filter(bound(?fourth))
        bind(?fourth as ?string)
      }
    }
  }
  bind(": " as ?sub)
  {
    # subbranch
    filter(regex(?string, ?sub))
    bind(strbefore(?string, ?sub) as ?denomination)
    bind(strafter(?string, ?sub) as ?subdenomination)
  } union {
    # no subbranch
    filter(!regex(?string, ?sub))
    bind(?string as ?denomination)
  }
}
group by ?cLabel ?denomination ?subdenomination
order by ?cLabel ?denomination ?subdenomination
