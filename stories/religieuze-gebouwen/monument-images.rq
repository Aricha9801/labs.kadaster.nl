prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix wde: <http://www.wikidata.org/entity/>
prefix wdn: <http://www.wikidata.org/prop/direct-normalized/>
prefix wdt: <http://www.wikidata.org/prop/direct/>
select distinct * {
  {
    select ?pand ?pandId ?wikidata (sample(?label) as ?label) (sample(?img) as ?img) {
      service <https://query.wikidata.org/sparql> {
        ?wikidata
          wdn:P5208 ?pand;
          wdt:P5208 ?pandId;
          wdt:P18 ?img;
          rdfs:label ?label;
          wdt:P31 wde:Q16970.
        filter(lang(?label) = 'nl')
      }
    }
    group by ?pand ?pandId ?wikidata
  }
  graph ?pandVoorkomen {
    ?pand bag:oorspronkelijkBouwjaar ?bouwjaar.
  }
  filter not exists {
    ?pandVoorkomen bag:eindGeldigheid [].
  }
  bind(substr(str(?wikidata),32) as ?wikidataId)
  bind('''
<p>Kerk: {{label}}</p>
<p>BAG bouwjaar: {{bouwjaar}} </p>
<p>BAG ID: <a href="{{pand}}" target="_blank">{{pandId}}</a></p>
<p>Wikidata ID: <a href="{{wikidata}}" target="_blank">{{wikidataId}}</a></p>
<img src="{{img}}" style="width: auto; max-width: none;">'''^^rdf:HTML as ?widget)
}
limit 10
