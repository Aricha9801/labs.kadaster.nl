prefix cbs: <http://betalinkeddata.cbs.nl/def/cbs#>
prefix cbs_83487: <http://betalinkeddata.cbs.nl/def/83487NED#>
prefix cbs_dim: <http://betalinkeddata.cbs.nl/def/dimension#>
prefix cbs_qb: <http://betalinkeddata.cbs.nl/def/cbs-qb#>
prefix cbs_slice: <http://betalinkeddata.cbs.nl/83487NED/id/slice/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix gwsw: <http://data.gwsw.nl/1.4/totaal/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?shape ?shapeLabel ?shapeColor {
  {
    service <https://sparql.gwsw.nl/repositories/TestDatastory> {
      ?rg a gwsw:Rioleringsgebied;
        gwsw:hasAspect ?ori;
        rdfs:label ?rg_name.
      ?ori a gwsw:Gebiedsorientatie;
        gwsw:hasAspect ?bg.
      ?bg a gwsw:Buitengrens;
        geo:asWKT ?shape.
      filter(contains(?rg_name, "Castricum"))
      bind('green' as ?shapeColor)
      bind(concat('<h3><a target="_blank" href="https://sparql.gwsw.nl/resource?uri=',str(?rg),'">',str(?rg_name),'</a></h3>') as ?shapeLabel)
    }
  } union {
    service <https://sparql.gwsw.nl/repositories/TestDatastory> {
      ?rg a gwsw:Rioleringsgebied;
        gwsw:hasAspect ?ori;
        rdfs:label ?shapelabel.
      ?ori a gwsw:Gebiedsorientatie;
        gwsw:hasAspect ?bg.
      ?bg a gwsw:Buitengrens;
        geo:asWKT ?shape_gwsw.
    }
    ?cbs_gemeente rdfs:label "Castricum"@nl.
    ?cbs_gemeente rdfs:label ?gemeente_name.
    ?buurt a cbs:Buurt;
      geo:hasGeometry ?geom;
      rdfs:label ?buurt_label;
      ?graphdb ?wijk.
    ?geom geo:asWKT ?shape.
    ?wijk a cbs:Wijk;
      ?graphdb ?cbs_gemeente.
    ?wijk rdfs:label ?wijk_name.
    filter(geof:sfIntersects(?shape, ?shape_gwsw))
    ?aantal_inwoners_obs
      cbs_dim:regio ?buurt;
      cbs_qb:inObservationGroup cbs_slice:bevolking_AantalInwoners;
      cbs_83487:bevolking_AantalInwoners ?aantal_inwoners.
    ?aantal_huishoudens_obs
      cbs_dim:regio ?buurt;
      cbs_qb:inObservationGroup cbs_slice:bevolking_ParticuliereHuishoudens_HuishoudensTotaal;
      cbs_83487:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ?aantal_huishoudens.
    optional {
      ?bedrijven
        cbs_dim:regio ?buurt;
        cbs_qb:inObservationGroup cbs_slice:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenTotaal;
        cbs_83487:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenTotaal ?aantal_bedrijven.
    ?recreatie
      cbs_dim:regio ?buurt;
      cbs_qb:inObservationGroup cbs_slice:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenNaarActiviteit_R-uCultuur_Recreatie_OverigeDiensten;
      cbs_83487:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenNaarActiviteit_R-uCultuur_Recreatie_OverigeDiensten ?aantal_recreatie.
    }
    bind('purple' as ?shapeColor)
    bind(concat(
      '<a href="',str(?buurt),'" target="_blank"><h3>',str(?buurt_label),'</h3></a>',
      '<p>Aantal inwoners: <a target="_blank" href="',str(?aantal_inwoners_obs),'">',str(?aantal_inwoners),'</a></p>',
      '<p>Aantal huishoudens: <a target="_blank" href="',str(?aantal_huishoudens_obs),'">',str(?aantal_huishoudens),'</p>') as ?shapeLabel)
  }
}
