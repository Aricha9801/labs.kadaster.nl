prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix gwsw: <http://data.gwsw.nl/1.4/totaal/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sesame: <http://www.openrdf.org/schema/sesame#>
select ?shape ?shapeColor ?shapeLabel {
  {
    service <https://sparql.gwsw.nl/repositories/TestDatastory> {
      select
        ?rg
        ?shape
        (concat('<h2><a target="_blank" href="',str(?rg),'">',str(?label),'</a></h2>',
                '<p>Totale leidinglengte: ',str(sum(?lengte)),'</p>') as ?shapeLabel)
      {
        ?rg a gwsw:Rioleringsgebied;
          rdfs:label ?label;
          gwsw:hasAspect ?ori.
        ?ori a gwsw:Gebiedsorientatie;
          gwsw:hasAspect ?bg.
        ?bg a gwsw:Buitengrens;
          geo:asWKT ?shape.
        filter(contains(?label, "Castricum"))
        ?rg gwsw:hasPart ?rs.
        ?rs gwsw:hasPart ?lei.
        ?lei a gwsw:Leiding;
          gwsw:hasAspect [
            a gwsw:LengteLeiding;
            gwsw:hasValue ?lengte ].
      }
      group by ?rg ?shape ?label
    }
  } union {
      select
        ?shape
        ("blue" as ?shapeColor)
        (concat("<h3><a target='_blank' href='https://sparql.gwsw.nl/resource?uri=", str(?put), "'>", str(?naam), "</a></h3><br>Stelsel: ", str(?rs_naam), "<br>Type uitlaat: <a target='_blank' href='" , str(?type), "'>", str(?type), "</a>") as ?shapeLabel)
      {
        ?rg a gwsw:Rioleringsgebied;
          rdfs:label ?label;
        filter(contains(?label, "Castricum"))
        ?rg gwsw:hasPart ?rs.
        ?rs rdfs:label ?rs_naam;
          gwsw:hasPart ?put.
        ?put a gwsw:Overstortput;
          sesame:directType ?type.
        filter(!(isBlank(?put)))
        filter(!(isBlank(?type)))
        ?put rdfs:label ?naam;
          gwsw:hasAspect [
            a gwsw:Putorientatie;
            gwsw:hasAspect [
              a gwsw:Punt;
              geo:asWKT ?shape ]].
    }
  } union  {
    select
      ?shape
      ("red" as ?shapeColor)
      (concat("<h3><a target='_blank' href='https://sparql.gwsw.nl/resource?uri=", str(?put), "'>", str(?naam), "</a></h3><br>Stelsel: ", str(?rs_naam), "<br>Type pomp: <a target='_blank' href='" , str(?type), "'>", str(?type), "</a>") as ?shapeLabel)
    {
      ?rg a gwsw:Rioleringsgebied;
        rdfs:label ?label;
      filter(contains(?label, "Castricum"))
      ?rg gwsw:hasPart ?rs.
      ?rs rdfs:label ?rs_naam;
        gwsw:hasPart ?put.
      ?put a gwsw:Pompput;
        sesame:directType ?type.
      filter(!(isBlank(?put)))
      filter(!(isBlank(?type)))
      ?put sesame:directType ?type;
        rdfs:label ?naam.
      ?put gwsw:hasAspect [
        a gwsw:Putorientatie;
        gwsw:hasAspect [
          a gwsw:Punt;
          geo:asWKT ?shape ]].
    }
  }
}
