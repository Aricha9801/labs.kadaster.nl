prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rvo: <https://internationaal.ondernemen.overheid.nl/def/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
select * {
  ?tekst
    dct:subject ?subcategorie, ?land;
    rvo:content ?content;
    rvo:do ?do.
  ?land a rvo:Land;
    foaf:depiction ?vlag;
    rdfs:label ?landLabel.
  ?subcategorie
    rdfs:label ?subcategorieLabel;
    skos:broader ?categorie.
  ?categorie rdfs:label ?categorieLabel.
  bind(if(?do,'✓','❌') as ?doLabel)
  bind('''
    <h1>{{categorieLabel}}</h1>
    <h2>{{subcategorieLabel}}</h2>
    <h3>Do or don't: {{doLabel}}</h3>
    {{content}}
    <img src="{{vlag}}">'''^^rdf:HTML as ?widget)
}
