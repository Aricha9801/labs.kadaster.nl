prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix dct: <http://purl.org/dc/terms/>
prefix hg: <http://rdf.histograph.io/>
prefix sdo: <http://schema.org/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix vcard: <http://www.w3.org/2006/vcard/ns#>
select (sum(?opp) as ?totaal_opp) {
  {
    service <https://api.labs.kadaster.nl/datasets/kadaster-dev/verkaufsbucher-story/services/verkaufsbucher-story/sparql> {
      ?tradeaction a sdo:TradeAction;
        sdo:object
          [ dct:type hg:Building;
            vcard:hasAddress
              [ hg:contains ?place;
                owl:sameAs ?bag_num;
                vcard:locality "Utrecht" ]].
      filter(?place != <http://archief.nl/NL-HaNA/id/land/Nederland>)
      optional {
        ?place owl:sameAs ?bag_wpl.
      }
    }
  }
  optional {
    ?bag_num bag:postcode ?postcode.
    graph ?vbog {
      ?bag_vbo bag:hoofdadres ?bag_num;
               bag:oppervlakte ?opp.
    }
    filter not exists {
      ?vbog bag:eindGeldigheid [].
    }
  }
}
